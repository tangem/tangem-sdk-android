apply from: '../artifactConfig.gradle'
apply plugin: 'maven-publish'

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = "$artifactConfig.group"
                artifactId = "$project.artifactId"
                version = "$artifactConfig.version".replace("/", "-")

                if (artifactId == "android") {
                    from components.release
                    artifact(sourceJar)

                    pom.withXml {
                        Node pomNode = asNode()
                        pomNode.dependencies.'*'.findAll() {
                            it.groupId.text() == artifactConfig.group
                        }.each() { it.parent().remove(it) }

                        pomNode.dependencies.'*'.findAll() {
                            it.scope.text() == 'runtime'
                        }.each { it.scope*.value = 'compile' }
                    }
                } else if (artifactId == "core") {
                    from components.java
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/tangem/tangem-sdk-android")
                credentials {
                    username = "$githubUser"
                    password = "$githubPass"
                }
            }
        }
    }
}